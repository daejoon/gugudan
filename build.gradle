buildscript {
    ext {
        kotlinVersion = '1.2.21'
        springBootVersion = '2.0.0.RELEASE'
        querydslVersion = '4.1.4'
        flywayVersion = '5.0.7'
        h2Version = '1.4.196'
        yamlVersion = '1.17'
        kotlinLoggingVersion = '1.4.9'
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")//기본은 final이다. open으로 변경한다.
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")// 해당 클래스들의 기본 생성자(Arguments가 없는것)을 생성한다.
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.h2database:h2:${h2Version}")
        classpath("gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:${flywayVersion}")
        classpath("org.yaml:snakeyaml:${yamlVersion}")
    }
}

/** plugins */
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.flywaydb.flyway'


/**
 * Spring.profiles.active
 * 프로파일이 존재하면 application-{profile}.yml을 로딩한다.
 * 프로파일이 존재하지 않는다면 application.yml을 로딩한다.
 */
final SPRING_CONFIG = {
    final PROFILE  = System.getProperty("spring.profiles.active")
    println "-Dspring.profiles.active='${PROFILE}'"

    def spring_config
    if (PROFILE) {
        spring_config = new org.yaml.snakeyaml.Yaml().load( new File("src/main/resources/application-${PROFILE}.yml").newInputStream())
    } else {
        spring_config = new org.yaml.snakeyaml.Yaml().load( new File("src/main/resources/application.yml").newInputStream())
    }

    return spring_config
}()


group = 'd2'
version = '0.0.3'
buildDir = 'out'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'd2.ApplicationKt'

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gugudan', 'Implementation-Version': version, 'Main-Class': mainClassName
    }
    dependsOn configurations.runtime
    from {
        configurations.compile.collect {it.isDirectory()? it: zipTree(it)}
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
    main.resources.srcDirs += 'src/main/resources'
}

idea {
    module {
        def kaptMain = file("${buildDir}/generated/source/kapt/main")
        sourceDirs += kaptMain
        generatedSourceDirs += kaptMain
    }
}

springBoot  {
    buildInfo()
}

flyway {
    url = SPRING_CONFIG.spring.datasource.url
    user = SPRING_CONFIG.spring.datasource.user
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")// require
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    runtime("org.springframework.boot:spring-boot-devtools")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    compile("com.querydsl:querydsl-core")
    compile("com.querydsl:querydsl-jpa")
    kapt("com.querydsl:querydsl-apt:${querydslVersion}:jpa")// 끝에 :jpa로 끝난다.
    compile("org.flywaydb:flyway-core")
    runtime("com.h2database:h2")
    compile("io.github.microutils:kotlin-logging:${kotlinLoggingVersion}")
}
